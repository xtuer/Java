group 'com.xtuer'
version '1.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.2.4'
    }
}

gretty {
    port = 8080
    contextPath = '/'
    servletContainer = 'tomcat7'

    debugSuspend = false
    managedClassReload      = true
    recompileOnSourceChange = false
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

////////////////////////////////////////////////////////////////////////////////
//                                   Maven 依赖                               //
////////////////////////////////////////////////////////////////////////////////
repositories {
    mavenLocal()
    mavenCentral()
}

ext.versions = [
    spring: '4.2.5.RELEASE',
    springSecurity: '4.0.4.RELEASE',
    springSessionDataRedis: '1.1.1.RELEASE',
    jstl: '1.2',
    servlet: '3.1.0',
    jackson: '2.5.3',
    freemarker: '2.3.20',
    logback: '1.1.2',
    jclOverSlf: '1.7.7'
]

dependencies {
    compile(
            "org.springframework:spring-webmvc:$versions.spring",             // Spring MVC
            "org.springframework:spring-context-support:$versions.spring",
            "org.springframework.security:spring-security-web:$versions.springSecurity", // Spring Security
            "org.springframework.security:spring-security-config:$versions.springSecurity",
            "com.fasterxml.jackson.core:jackson-databind:$versions.jackson",  // JSON
            "org.freemarker:freemarker:$versions.freemarker",                 // Freemarker
            "ch.qos.logback:logback-classic:$versions.logback",               // Logback
            "org.slf4j:jcl-over-slf4j:$versions.jclOverSlf",
            "org.springframework.session:spring-session-data-redis:$versions.springSessionDataRedis" // Spring Session
    )

    compile("javax.servlet:jstl:$versions.jstl") // JSTL
    compileOnly("javax.servlet:javax.servlet-api:$versions.servlet")
}
