group 'com.xtuer'
version '1.0'

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

buildscript {
    repositories { jcenter() }
    dependencies { classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0' }
}

mainClassName = 'PaperMetaImporter' // TODO: 如果不是可执行 jar 包，不需要

jar {
    manifest { attributes 'Main-Class': mainClassName }
}

// 打包命令: gradle clean shadowJar
shadowJar {
    mergeServiceFiles('META-INF/spring.*')
    archiveName = 'import-admin.zip'
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.springframework:spring-context-support:4.3.0.RELEASE'
    compile 'org.springframework:spring-jdbc:4.3.0.RELEASE'
    compile 'com.alibaba:druid:1.0.27'
    compile 'mysql:mysql-connector-java:5.1.21'
    compile 'org.mybatis:mybatis:3.2.1'
    compile 'org.mybatis:mybatis-spring:1.2.2'

    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'ch.qos.logback:logback-classic:1.1.2'
    compile 'com.alibaba:fastjson:1.2.29'
    compile 'org.projectlombok:lombok:1.16.10'
    compile 'org.yaml:snakeyaml:1.18'
    compile 'commons-io:commons-io:2.5'
    compile 'org.apache.commons:commons-csv:1.4'

    testCompile('junit:junit:4.12')
    testCompile('org.springframework:spring-test:4.3.0.RELEASE')
}

ext {
    // 运行和打包的环境选择, 默认是开发环境
    // 获取 gradle 参数中 env 的值: gradle -Denv=production clean build
    // 构建 gradle clean build
    //     gradle -Denv=production clean build
    // 部署 gradle clean deploy
    //     gradle -Denv=production clean deploy
    environment = System.getProperty("env", "development")
}

////////////////////////////////////////////////////////////////////////////////
//                                  资源动态替换                                //
////////////////////////////////////////////////////////////////////////////////
processResources {
    // src/main/resources 下的文件中 @key@ 的内容使用 config.groovy 里对应的进行替换
    println "==> Load configuration for '${environment}'"
    def configFile = file('config.groovy') // 配置文件
    def props = new groovy.util.ConfigSlurper(environment).parse(configFile.toURI().toURL()).toProperties()

    from(sourceSets.main.resources.srcDirs) {
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: props)
    }
}

// 创建目录结构的任务，执行后可删除
task createStructure {
    doLast {
        sourceSets*.java.srcDirs*.each { it.mkdirs() }
        sourceSets*.resources.srcDirs*.each { it.mkdirs() }
    }
}

/*
生成的目录结构
├── build.gradle
└── src
    ├── main
    │   ├── java
    │   └── resources
    └── test
        ├── java
        └── resources
*/
