plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

////////////////////////////////////////////////////////////////////////////////
//                                [1] [2] 运行、打包                           //
////////////////////////////////////////////////////////////////////////////////
// [1.1] 从命令行运行默认类: gradle run
// [1.2] 从命令行运行某个类: gradle run -DmainClass=Foo
ext {
    project.mainClassName = System.getProperty("mainClass", "Test")
}

// [2] 打包: gradle clean shadowJar [-DmainClass=Foo]
shadowJar {
    mergeServiceFiles('META-INF/spring.*')
    archiveName = 'app.jar' // 输出的 jar 包名字
}

////////////////////////////////////////////////////////////////////////////////
//                                 [3] Maven 依赖                             //
////////////////////////////////////////////////////////////////////////////////
repositories {
    mavenCentral()
}

ext.versions = [
        spring        : '5.0.2.RELEASE',
        springSecurity: '5.0.0.RELEASE',
        springSession : '2.0.0.RELEASE',
        redisClient   : '2.9.0',
        servlet       : '4.0.0',
        lombok        : '1.16.20',
        fastjson      : '1.2.68',
        thymeleaf     : '3.0.9.RELEASE',
        mysql         : '5.1.46',
        mybatis       : '3.5.0',
        mybatisSpring : '1.3.1',
        druid         : '1.1.17',
        validator     : '6.0.5.Final',
        commonsLang   : '3.7',
        commonsFileupload: '1.3.3',
        snakeyaml     : '1.19',
        easyOkHttp    : '1.1.3',
        easyPoi       : '3.0.3',
        logback       : '1.3.0-alpha4',
        slf4j         : '1.7.25',
        junit         : '4.12',
        jsoup         : '1.11.3',
        prettyTime    : '4.0.1.Final',
        jodconverter  : '4.2.0',
        mongodb       : '2.1.2.RELEASE',
        hanlp         : 'portable-1.6.4',
        elasticsearch : '3.1.2.RELEASE',
        ffmpeg        : '0.6.2',
        activeMq      : '5.7.0',
        activeMqPool  : '5.14.1',
        jmsApi        : '2.0.1',
        caffeine      : '2.7.0',
        curator       : '2.12.0'
]

dependencies {
    compile(
            "org.springframework:spring-core:${versions.spring}",
            "org.springframework:spring-context:${versions.spring}",
            "org.springframework:spring-context-support:${versions.spring}",
            "org.springframework:spring-jdbc:${versions.spring}",
            "org.mybatis:mybatis-spring:${versions.mybatisSpring}",
            "org.mybatis:mybatis:${versions.mybatis}",
            "com.alibaba:druid:${versions.druid}",
            "mysql:mysql-connector-java:${versions.mysql}",
            "org.apache.commons:commons-lang3:${versions.commonsLang}",
            "com.alibaba:fastjson:${versions.fastjson}",
    )

    // Log dependencies
    compile(
            "ch.qos.logback:logback-classic:${versions.logback}", // slf4j-logback 绑定
            "org.slf4j:jcl-over-slf4j:${versions.slf4j}",         // redirect apache commons logging
            "org.slf4j:jul-to-slf4j:${versions.slf4j}",           // redirect jdk util logging
            "org.slf4j:log4j-over-slf4j:${versions.slf4j}",       // redirect log4j
    )

    testCompile 'junit:junit:4.12'
    compileOnly 'org.projectlombok:lombok:1.16.18'
    annotationProcessor 'org.projectlombok:lombok:1.16.18'
}

////////////////////////////////////////////////////////////////////////////////
//                                    JVM                                     //
////////////////////////////////////////////////////////////////////////////////
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}
