group 'com.xtuer'
version '1.0'

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:1.2.4'
    }
}

gretty {
    port = 8080
    contextPath = '/'
    servletContainer = 'tomcat7'

    inplaceMode = 'hard'
    debugSuspend = false
    managedClassReload      = true
    recompileOnSourceChange = false
}

tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

////////////////////////////////////////////////////////////////////////////////
//                                   Maven 依赖                               //
////////////////////////////////////////////////////////////////////////////////
repositories {
    mavenLocal()
    mavenCentral()
}

ext.versions = [
    spring: '4.3.0.RELEASE',
    springMobile: '1.1.5.RELEASE',
    jstl: '1.2',
    servlet: '3.1.0',
    jackson: '2.7.4',
    freemarker: '2.3.23'
]

dependencies {
    compile(
            "org.springframework:spring-webmvc:$versions.spring",             // Spring MVC
            "org.springframework:spring-context-support:$versions.spring",
            "org.springframework.mobile:spring-mobile-device:$versions.springMobile",
            "com.fasterxml.jackson.core:jackson-databind:$versions.jackson",  // JSON
            "org.freemarker:freemarker:$versions.freemarker",                 // Freemarker
            "commons-fileupload:commons-fileupload:1.3.1"
    )

    compile("javax.servlet:jstl:$versions.jstl") // JSTL
    compileOnly("javax.servlet:javax.servlet-api:$versions.servlet")
}
